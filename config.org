#+TITLE: Havner's Emacs configuration

* Packages
** Sources
Moved to ~/.emacs.d/init.el. Autoinstall selected-packages if not installed.

** Hand addons (helpa)
#+BEGIN_SRC emacs-lisp
(add-to-list 'load-path "~/.emacs.d/helpa/zenburn-emacs")
(add-to-list 'load-path "~/.emacs.d/helpa/spacemacs-theme")
(add-to-list 'load-path "~/.emacs.d/helpa/emacs-color-theme-solarized")
#+END_SRC

* Functions
** Configurability
#+BEGIN_SRC emacs-lisp
(defun havner/open-config-org (&optional arg)
  (interactive "P")
  (let ((config "~/.emacs.d/config.org"))
    (if arg
        (find-file-other-window config)
      (find-file config))))
(global-set-key (kbd "<f12>") #'havner/open-config-org)

(defun havner/set-ubuntu-font-size (&optional arg)
  (interactive "NFont size: ")
  (let ((font-to-set (concat "Ubuntu Mono-" (number-to-string arg))))
    (set-face-attribute 'default nil :font font-to-set)))
(global-set-key (kbd "M-<f12>") #'havner/set-ubuntu-font-size)

(defun havner/set-font-size (&optional arg)
  (interactive "NFont size: ")
  (set-face-attribute 'default nil :height (* arg 10)))
(global-set-key (kbd "C-<f12>") #'havner/set-font-size)
#+END_SRC

** Mac / OSX bootstrap
#+BEGIN_SRC emacs-lisp
;;; get rid of the travesty set in term/ns-win
(when (eq window-system 'ns)
  (define-key global-map [home] 'move-beginning-of-line)
  (define-key global-map [end]  'move-end-of-line))

;;; unify mac distributions with other OS'es
(cl-case window-system
  ('mac
   (setq mac-option-modifier '(:function super :mouse super))
   (setq mac-command-modifier 'meta))
  ('ns
   (setq ns-option-modifier '(:function super :mouse super))
   (setq ns-command-modifier 'meta)))

;;; Handle mac keyboard without insert
;;; <help> -> insert on regular PC keyboard
;;; <f13> -> above "insert" on full mac keyboard
;;; <clear> -> on numpad on full mac keyboard
(when (or (eq window-system 'mac)
          (eq window-system 'ns))
  (define-key function-key-map (kbd "<help>") (kbd "<insert>"))
  (global-set-key (kbd "<S-help>") #'yank)
  (global-set-key (kbd "<C-help>") #'kill-ring-save)
  (define-key function-key-map (kbd "<f13>") (kbd "<insert>"))
  (global-set-key (kbd "<S-f13>") #'yank)
  (global-set-key (kbd "<C-f13>") #'kill-ring-save)
  (define-key function-key-map (kbd "<clear>") (kbd "<insert>"))
  (global-set-key (kbd "<S-clear>") #'yank)
  (global-set-key (kbd "<C-clear>") #'kill-ring-save)
  (with-eval-after-load 'term
    (define-key term-raw-map (kbd "<help>") #'term-send-insert)
    (define-key term-raw-map (kbd "<S-help>") #'term-paste)
    (define-key term-raw-map (kbd "<f13>") #'term-send-insert)
    (define-key term-raw-map (kbd "<S-f13>") #'term-paste)
    (define-key term-raw-map (kbd "<clear>") #'term-send-insert)
    (define-key term-raw-map (kbd "<S-clear>") #'term-paste)))

(defun browse-url-open (url &optional ignored)
  "Pass the specified URL to the \"open\" command.
open is a OSX desktop utility that calls your preferred web browser.
The optional argument IGNORED is not used."
  (interactive (browse-url-interactive-arg "URL: "))
  (call-process "open" nil 0 nil url))

(defun havner/toggle-frame-fullscreen ()
  "Toggle fullscreen state of selected frame."
  (interactive)
  (let ((fullscreen (frame-parameter nil 'fullscreen)))
    (if (memq fullscreen '(fullscreen fullboth))
        (let ((fullscreen-restore (frame-parameter nil 'fullscreen-restore)))
          (if (memq fullscreen-restore '(maximized fullheight fullwidth))
              (set-frame-parameter nil 'fullscreen fullscreen-restore)
            (set-frame-parameter nil 'fullscreen nil)))
      (set-frame-parameter nil `fullscreen 'fullscreen))))

;;; Use the F11 as the true macos fullscreen
(when (or (eq window-system 'mac)
          (eq window-system 'ns))
  (advice-add 'toggle-frame-fullscreen
              :override #'havner/toggle-frame-fullscreen))
#+END_SRC

** Elisp
#+BEGIN_SRC emacs-lisp
(defun add-to-list-global (list elem &optional app)
  (if app
      (set-default list (append (eval list) `(,elem)))
    (set-default list (append `(,elem) (eval list)))))

(defun delete-from-list (list elem)
  (set list (delete elem (eval list))))
#+END_SRC

** To be used directly (interactive)
#+BEGIN_SRC emacs-lisp
(defun havner/kill-buffers-prefix (arg)
  (interactive "MPrefix: ")
  (let ((buffers (buffer-list)))
    (while buffers
      (let ((buffer (car buffers)))
        (if (string-prefix-p arg (buffer-name buffer) t)
            (kill-buffer buffer)))
      (setq buffers (cdr buffers)))))

(defun havner/kill-buffers-magit ()
  (interactive)
  (havner/kill-buffers-prefix "magit"))

(defun havner/kill-buffers-helm ()
  (interactive)
  (let ((buffers (buffer-list)))
    (while buffers
      (let* ((buffer (car buffers))
             (bname (buffer-name buffer)))
        (if (and (string-prefix-p "*helm" bname t)
                 (not (string-equal bname helm-last-buffer)))
            (kill-buffer buffer)))
      (setq buffers (cdr buffers)))))

(defun havner/find-file-as-sudo ()
  "Open currently opened file with sudo."
  (interactive)
  (let ((file-name (buffer-file-name)))
    (when file-name
      (find-alternate-file (concat "/sudo::" file-name)))))

(defvar havner/lang-ring
  "List of languages the `havner/cycle-ispell-languages' will cycle through.")
(let ((langs '("polish" "english")))
  (setq havner/lang-ring (make-ring (length langs)))
  (dolist (elem langs) (ring-insert havner/lang-ring elem))
  (ispell-change-dictionary (ring-ref havner/lang-ring -1)))

(defun havner/cycle-ispell-languages ()
  "Cycle currently used Ispell language from `havner/lang-ring'."
  (interactive)
  (let ((lang (ring-ref havner/lang-ring -1)))
    (ring-insert havner/lang-ring lang)
    (ispell-change-dictionary lang)))

(defun havner/display-prefix (arg)
  "Display the value of the raw prefix ARG."
  (interactive "P")
  (message "%s" arg))

(defun havner/de-unicode ()
  "Tidy up a buffer by replacing all special Unicode characters.
Replaces things like smart quotes with their more sane cousins."
  (interactive)
  (let ((unicode-map '(("[\u2018\|\u2019\|\u201A\|\uFFFD]" . "'")
                       ("[\u201c\|\u201d\|\u201e]" . "\"")
                       ("\u2013" . "--")
                       ("\u2014" . "---")
                       ("\u2026" . "...")
                       ("\u00A9" . "(c)")
                       ("\u00AE" . "(r)")
                       ("\u2122" . "TM")
                       ("[\u02DC\|\u00A0]" . " "))))
    (save-excursion
      (cl-loop for (key . value) in unicode-map
               do
               (goto-char (point-min))
               (replace-regexp key value)))))

(defun havner/eval-and-replace ()
  "Replace the preceding sexp with its value."
  (interactive)
  (backward-kill-sexp)
  (condition-case nil
      (prin1 (eval (read (current-kill 0)))
             (current-buffer))
    (error (message "Invalid expression")
           (insert (current-kill 0)))))

(defun havner/write-file-and-delete ()
  "Write file under new name and delete the old file."
  (interactive)
  (let ((old-name (buffer-name))
        (old-file-name (buffer-file-name)))
    (if (not old-file-name)
        (message "'%s' is not a file!" old-name)
      (progn
        (call-interactively 'write-file)
        (delete-file old-file-name)))))
#+END_SRC

** CMD Line
#+BEGIN_SRC emacs-lisp
(defun havner/command-line-ediff (switch)
  "EDiff two files from command line"
  (let ((file1 (pop command-line-args-left))
        (file2 (pop command-line-args-left)))
    (ediff file1 file2)))
(add-to-list 'command-switch-alist '("ediff" . havner/command-line-ediff))

(defun havner/command-line-vdiff (switch)
  "VDiff two files from command line"
  (let ((file1 (pop command-line-args-left))
        (file2 (pop command-line-args-left)))
    (vdiff-files file1 file2)))
(add-to-list 'command-switch-alist '("vdiff" . havner/command-line-vdiff))
#+END_SRC

** Themes
#+BEGIN_SRC emacs-lisp
(defun havner/disable-themes ()
  (interactive)
  (dolist (theme custom-enabled-themes)
    (if theme (disable-theme theme))))

(defmacro havner/def-theme-function (fun-name desc module theme &rest body)
  `(defun ,fun-name ()
     ,desc
     (interactive)
     (require ,module)
     (havner/disable-themes)
     ,@body
     (load-theme ,theme t)))

;;;                        FUNCTION-NAME           FUNCTION-DESC      FILE-NAME              THEME-NAME

(havner/def-theme-function havner/solarized-dark   "Solarized Dark"   'solarized-definitions 'solarized
                           (setq frame-background-mode 'dark)
                           (mapc 'frame-set-background-mode (frame-list)))
(havner/def-theme-function havner/solarized-light  "Solarized Light"  'solarized-definitions 'solarized
                           (setq frame-background-mode 'light)
                           (mapc 'frame-set-background-mode (frame-list)))

(havner/def-theme-function havner/spacemacs-dark   "Spacemacs Dark"   'spacemacs-common      'spacemacs-dark)
(havner/def-theme-function havner/spacemacs-light  "Spacemacs Light"  'spacemacs-common      'spacemacs-light)

(havner/def-theme-function havner/zenburn          "Zenburn"          'zenburn-theme         'zenburn)

(havner/def-theme-function havner/doom-one         "DOOM One"         'doom-themes           'doom-one)
(havner/def-theme-function havner/doom-one-light   "DOOM One Light"   'doom-themes           'doom-one-light)
(havner/def-theme-function havner/doom-city-lights "DOOM City Lights" 'doom-themes           'doom-city-lights)
(havner/def-theme-function havner/doom-vibrant     "DOOM Vibrant"     'doom-themes           'doom-vibrant)
(havner/def-theme-function havner/doom-moonlight   "DOOM Moonlight"   'doom-themes           'doom-moonlight)
#+END_SRC

** Additional basic window/frame functions
#+BEGIN_SRC emacs-lisp
(defun kill-current-buffer ()
  "Kill the current buffer without prompting."
  (interactive)
  (kill-buffer (current-buffer)))

(defun kill-buffer-and-window-and-balance ()
  "Kill buffer and window and balance"
  (interactive)
  (kill-buffer-and-window)
  (balance-windows))

(defun delete-window-and-balance ()
  "Delete current windowKill the current buffer without prompting."
  (interactive)
  (delete-window)
  (balance-windows))

(defun split-window-below-switch-and-balance ()
  "Split the window horizontally, then switch to the new pane."
  (interactive)
  (split-window-below)
  (other-window 1)
  (balance-windows))

(defun split-window-right-switch-and-balance ()
  "Split the window vertically, then switch to the new pane."
  (interactive)
  (split-window-right)
  (other-window 1)
  (balance-windows))
#+END_SRC

** switch-window variants
#+BEGIN_SRC emacs-lisp
(autoload 'switch-window--then "switch-window")

(defun switch-window-then-kill-current-buffer ()
  (interactive)
  (switch-window--then
   "Buffer to kill: "
   #'kill-current-buffer
   #'kill-current-buffer t))

(defun switch-window-then-kill-buffer-and-window-and-balance ()
  (interactive)
  (switch-window--then
   "Window to kill: "
   #'kill-buffer-and-window-and-balance
   #'kill-buffer-and-window-and-balance t))

(defun switch-window-then-delete-window-and-balance ()
  (interactive)
  (switch-window--then
   "Delete window: "
   #'delete-window-and-balance
   #'delete-window-and-balance t))

(defun switch-window-then-split-below-switch-and-balance (arg)
  (interactive "P")
  (switch-window--then
   "Below-split window: "
   #'split-window-below-switch-and-balance
   #'split-window-below-switch-and-balance arg 1))

(defun switch-window-then-split-right-switch-and-balance (arg)
  (interactive "P")
  (switch-window--then
   "Right-split window: "
   #'split-window-right-switch-and-balance
   #'split-window-right-switch-and-balance arg 1))
#+END_SRC

** Option to delete trailing whitespace on file save
#+BEGIN_SRC emacs-lisp
(defcustom delete-trailing-whitespace-on-save nil
  "Whether to call `delete-trailing-whitespace' on file save."
  :type 'boolean
  :group 'havner)

(defun maybe-delete-trailing-whitespace ()
  (when (and delete-trailing-whitespace-on-save
             (not (eq major-mode 'diff-mode)))
    (delete-trailing-whitespace)))

(with-eval-after-load 'files
  (add-hook 'before-save-hook #'maybe-delete-trailing-whitespace))
#+END_SRC

** Option to restore EDiff state on exit
#+BEGIN_SRC emacs-lisp
(defcustom ediff-restore-winconfig-state-on-exit nil
  "Whether to restore a previous winconfig state after quitting EDiff."
  :type 'boolean
  :group 'havner)

(defvar ediff-last-winconfig nil)
(defun ediff-maybe-save-winconfig-state ()
  (if ediff-restore-winconfig-state-on-exit
      (setq ediff-last-winconfig (current-window-configuration))))
(defun ediff-maybe-restore-winconfig-state ()
  (if ediff-restore-winconfig-state-on-exit
      (set-window-configuration ediff-last-winconfig)))

(with-eval-after-load 'ediff-init
  (add-hook 'ediff-before-setup-hook #'ediff-maybe-save-winconfig-state)
  (add-hook 'ediff-quit-hook #'ediff-maybe-restore-winconfig-state))
#+END_SRC

** Option to replace completion at point with company
#+BEGIN_SRC emacs-lisp
(defcustom company-replace-completion nil
  "Whether to use company-complete every time completion-at-point is called."
  :type 'boolean
  :group 'havner)

(defun company-maybe-replace-completion (orig-fun &rest args)
  (if (or (not company-replace-completion)
          (eq (active-minibuffer-window)
              (selected-window)))
      (apply orig-fun args)
    (company-complete)))

(advice-add 'completion-at-point
            :around #'company-maybe-replace-completion)
#+END_SRC

** Option for magit not to restore window configuration
#+BEGIN_SRC emacs-lisp
(defcustom magit-dont-restore-window-configuration nil
  "Whether not to restore windows configuration on magit quit."
  :type 'boolean
  :group 'havner)

(defun magit-maybe-dont-restore-window-configuration (orig-fun &rest args)
  "Bury or kill the current buffer and DON'T restore previous window configuration."
  (if magit-dont-restore-window-configuration
      (quit-window (car args) (selected-window))
    (apply orig-fun args)))

(advice-add 'magit-restore-window-configuration
            :around #'magit-maybe-dont-restore-window-configuration)
#+END_SRC

* Configuration
#+BEGIN_SRC emacs-lisp
(defvar havner/completing 'helm)
#+END_SRC

** Themes
*** Zenburn
#+BEGIN_SRC emacs-lisp
#+END_SRC

*** Spacemacs
#+BEGIN_SRC emacs-lisp
(setq spacemacs-theme-comment-bg nil)
(setq spacemacs-theme-comment-italic nil)
(setq spacemacs-theme-underline-parens nil)
(setq spacemacs-theme-org-height nil)
#+END_SRC

*** Solarized
#+BEGIN_SRC emacs-lisp
(setq solarized-termcolors 256)
#+END_SRC

*** Doom
#+BEGIN_SRC emacs-lisp
;; (doom-themes-visual-bell-config)
(setq doom-themes-enable-bold t)     ; if nil, bold is universally disabled
(setq doom-themes-enable-italic nil) ; if nil, italics is universally disabled
#+END_SRC

*** LOAD
#+BEGIN_SRC emacs-lisp
(defvar havner/colors)

(cond (window-system
       (setq havner/colors '24bit))
      ((equal (getenv "TERM") "xterm-24bit")
       (setq havner/colors '24bit))
      ((equal (getenv "TERM") "xterm-256color")
       (setq havner/colors '256color))
      ((equal (getenv "TERM") "xterm-16color")
       (setq havner/colors '16color))
      ((equal (getenv "TERM") "xterm")
       (setq havner/colors '8color))
      ((equal (getenv "TERM") "linux")
       (setq havner/colors '8color))
      (t
       (setq havner/colors 'headless)))

(cl-case havner/colors
  ('24bit    (havner/doom-moonlight))
  ('256color (havner/zenburn)))
#+END_SRC

** Misc options
#+BEGIN_SRC emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)   ; Treat 'y' or <CR> as yes, 'n' as no.
(define-key query-replace-map [return] 'act)
(define-key query-replace-map [?\C-m] 'act)

(setq load-prefer-newer t)
(setq inhibit-startup-screen t)
(setq scroll-conservatively 101)
(setq scroll-error-top-bottom t)
(setq require-final-newline t)
(setq gc-cons-threshold (* 10 1024 1024))
(setq-default truncate-lines t)
(setq bookmark-default-file "~/.emacs-bookmarks.el")
(setq recentf-save-file "~/.emacs-recentf.el")
(setq recentf-max-saved-items 500)
(setq create-lockfiles nil)       ; lockfiles breaks python completion
(setq find-file-visit-truename t) ; doom-modeline likes that
(setq delete-trailing-whitespace-on-save t) ; my own option

(when window-system
  (setq confirm-kill-emacs 'y-or-n-p))

;;; minor modes
(setq show-paren-delay 0.0)
(setq display-time-24hr-format t)
(setq display-time-day-and-date nil)
(setq display-time-default-load-average nil)

;;; hooks
(add-hook 'text-mode-hook #'turn-on-auto-fill)
(add-hook 'after-save-hook #'executable-make-buffer-file-executable-if-script-p)
#+END_SRC

** Minor modes
#+BEGIN_SRC emacs-lisp
;;; GUI
(menu-bar-mode 0)
(tool-bar-mode 0)
(tooltip-mode 0)

(when window-system
  (set-scroll-bar-mode nil))

;;; modeline
(column-number-mode t)
(line-number-mode t)
(size-indication-mode t)
(display-time-mode t)

;;; misc / buffer
(show-paren-mode t)
(delete-selection-mode t)
(transient-mark-mode t)
(global-auto-revert-mode t)
(recentf-mode t)

;;; external, too short for their own section
(global-page-break-lines-mode t)
(beginend-global-mode t)
#+END_SRC

** GUI options
#+BEGIN_SRC emacs-lisp
(setq use-dialog-box t)
(setq default-frame-alist
      '((width . 120)
        (height . 40)))
(setq-default cursor-type 'bar)
(cl-case window-system
  ('w32 (set-face-attribute 'default nil :font "Ubuntu Mono-12"))
  ('mac (set-face-attribute 'default nil :height 140))
  ('ns  (set-face-attribute 'default nil :family "Monaco" :height 140)))
#+END_SRC

** Mouse options
#+BEGIN_SRC emacs-lisp
(setq mouse-yank-at-point t)
(setq mouse-wheel-scroll-amount '(1 ((shift) . 5) ((control))))

(when (eq window-system 'mac)
  (setq mac-mouse-wheel-smooth-scroll nil))

(xterm-mouse-mode t)
#+END_SRC

** Backups
#+BEGIN_SRC emacs-lisp
(setq temporary-file-directory "~/tmp")
(unless (file-directory-p temporary-file-directory)
  (mkdir temporary-file-directory))

(setq backup-directory-alist
      `((".*" . ,temporary-file-directory)))
(setq auto-save-list-file-prefix
      (concat temporary-file-directory "/auto-save-list/.saves-"))
;; (setq auto-save-file-name-transforms
;;       `((".*" ,temporary-file-directory t)))
#+END_SRC

** Tab related
#+BEGIN_SRC emacs-lisp
(setq tab-always-indent 'complete)
(setq backward-delete-char-untabify-method nil)
(setq-default indent-tabs-mode t)
(setq-default tab-width 4)
(defvaralias 'standard-indent 'tab-width)
#+END_SRC

** paradox
#+BEGIN_SRC emacs-lisp
(setq paradox-column-width-package 30)
(setq paradox-column-width-version 14)
(setq paradox-spinner-type 'progress-bar-filled)
(setq paradox-automatically-star nil)
(setq package-native-compile t)
#+END_SRC

** shackle
#+BEGIN_SRC emacs-lisp
;;; Finally, don't create/switch/delete windows uncontrollably because
;;; every plugin author has a different view on how your workflow
;;; should look like. With few small exceptions (popup windows) don't
;;; create any windows unless I do that explicitely.

(setq shackle-rules '(("^\*helm" :regexp t)
                      ("^\*magit.*popup\*" :regexp t)
                      ("\*transient\*" :regexp t)))
(setq shackle-default-rule '(:same t :inhibit-window-quit t :select t))

(shackle-mode t)
#+END_SRC

** switch-window
#+BEGIN_SRC emacs-lisp
(setq switch-window-minibuffer-shortcut ?x)
(setq switch-window-background t)
;; (setq switch-window-multiple-frames t)
(setq switch-window-mvborder-increment 2)
(cl-case havner/completing
  ('helm
   (setq switch-window-preferred 'helm)))
#+END_SRC

** window-jump
#+BEGIN_SRC emacs-lisp
(defvaralias 'wj-jump-frames 'switch-window-multiple-frames)
#+END_SRC

** vundo
#+BEGIN_SRC emacs-lisp
(setq vundo-compact-display t)
(setq vundo-window-max-height 15)
#+END_SRC

** dired
#+BEGIN_SRC emacs-lisp
(setq dired-dwim-target t)
(setq dired-auto-revert-buffer t)
(setq dired-listing-switches "-alhB --group-directories-first")
(if (eq system-type 'darwin)
    (setq insert-directory-program "gls"))

(if (eq system-type 'windows-nt)
    (setq ls-lisp-use-insert-directory-program t))
#+END_SRC

** whitespace-mode
#+BEGIN_SRC emacs-lisp
(setq whitespace-line-column 80)
(cl-case havner/colors
  ('24bit    (setq whitespace-style '(face trailing tabs spaces lines-tail space-mark tab-mark)))
  ('256color (setq whitespace-style '(face trailing tabs spaces lines-tail space-mark tab-mark)))
  (t         (setq whitespace-style '(face trailing lines-tail tab-mark))))
#+END_SRC

** parinfer-rust-mode
#+BEGIN_SRC emacs-lisp
(setq parinfer-rust-preferred-mode "indent")

;;; use rainbow-delimeters in non paren modes as old parinfer did
(with-eval-after-load 'parinfer-rust-helper
  (defun parinfer-rust--dim-parens ()
   "Apply paren dimming if appropriate."
   (if (and parinfer-rust-enabled
            (not (string-equal parinfer-rust--mode "paren"))
            parinfer-rust-dim-parens)
       (progn
         (when (bound-and-true-p rainbow-delimiters-mode)
           (rainbow-delimiters-mode-disable))
         (font-lock-add-keywords
          nil '((parinfer-rust--dim-parens-fontify-search . 'parinfer-rust-dim-parens))))
     (font-lock-remove-keywords
      nil '((parinfer-rust--dim-parens-fontify-search . 'parinfer-rust-dim-parens)))
     (when (fboundp 'rainbow-delimiters-mode)
       (rainbow-delimiters-mode-enable)))
   (parinfer-rust--dim-parens-refresh)))

(with-eval-after-load 'parinfer-rust-mode
  (define-key parinfer-rust-mode-map (kbd "C-c C-p") nil))
#+END_SRC

** buffer-show / ibuffer
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'bs
  (add-to-list 'bs-configurations '("havner" "^\\*ansi-term\\*" nil nil bs-visits-non-file bs--sort-by-name))
  (setq bs-default-configuration "havner"))

(setq ibuffer-view-ibuffer t)
#+END_SRC

** which-key
#+begin_src emacs-lisp
(which-key-mode t)
#+end_src

** calendar
#+BEGIN_SRC emacs-lisp
(setq calendar-week-start-day 1)
(setq calendar-mark-holidays-flag t)
(with-eval-after-load 'calendar
  (calendar-set-date-style 'european)
  (add-hook 'calendar-today-visible-hook 'calendar-mark-today))

(setq holiday-hebrew-holidays nil)
(setq holiday-islamic-holidays nil)
(setq holiday-bahai-holidays nil)
(setq holiday-oriental-holidays nil)
(setq holiday-christian-holidays nil)
(setq holiday-general-holidays
      `((holiday-fixed 1 1 "Nowy Rok")
        (holiday-fixed 1 6 "Trzech Króli")
        (holiday-easter-etc 0 "Wielkanoc")
        (holiday-easter-etc 1 "Poniedziałek Wielkanocny")
        (holiday-fixed 5 1 "Święto Pracy")
        (holiday-fixed 5 3 "Święto Konstytucji 3 Maja")
        (holiday-easter-etc 49 "Zielone świątki")
        (holiday-easter-etc 60 "Boże Ciało")
        (holiday-fixed 8 15 "Wniebowzięcie Najświętrzej Maryi Panny")
        (holiday-fixed 11 1 "Wszystkich Świętych")
        (holiday-fixed 11 11 "Święto Niepodległości")
        (holiday-fixed 12 25 "Pierwszy dzień Bożego Narodzenia")
        (holiday-fixed 12 26 "Drugi dzień Bożego Narodzenia")))
#+END_SRC

** flyspell
#+BEGIN_SRC emacs-lisp
(cl-case havner/completing
  ('helm
   (with-eval-after-load 'flyspell-correct
     (require 'flyspell-correct-helm)))
  (t
   (with-eval-after-load 'flyspell-correct
     (require 'flyspell-correct-popup))))
#+END_SRC

** delight
#+BEGIN_SRC emacs-lisp
(delight '(
           (beginend-global-mode nil "beginend")
           (beginend-bs-mode nil "beginend")
           (beginend-prog-mode nil "beginend")
           (beginend-dired-mode nil "beginend")
           (beginend-org-agenda-mode nil "beginend")
           (beginend-compilation-mode nil "beginend")
           (beginend-magit-status-mode nil "beginend")
           (beginend-prodigy-mode nil "beginend")
           (beginend-vc-dir-mode nil "beginend")
           (beginend-ibuffer-mode nil "beginend")
           (beginend-org-mode nil "beginend")
           (beginend-outline-mode nil "beginend")
           (org-indent-mode nil "org-indent")
           (company-mode nil "company")
           (helm-mode nil "helm-mode")
           (page-break-lines-mode nil "page-break-lines")
           (subword-mode nil "subword")
           (auto-revert-mode nil "autorevert")
           (auto-fill-function nil "simple")
           (abbrev-mode nil "abbrev")
           (eldoc-mode nil "eldoc")
           (hs-minor-mode nil "hideshow")
           (symbol-overlay-mode nil "symbol-overlay")
           (yas-minor-mode nil "yasnippet")
           (anzu-mode nil "anzu")
           (projectile-mode nil "projectile")
           (helm-ff-cache-mode nil "helm-files")
           ))
#+END_SRC

** vterm
#+BEGIN_SRC emacs-lisp
(autoload 'multi-vterm-next "multi-vterm")
#+END_SRC

** AVY
#+BEGIN_SRC emacs-lisp
(setq avy-keys (append (number-sequence ?a ?z) (number-sequence ?A ?Z)))
(setq avy-background t)

(autoload 'avy-pop-mark "avy")
#+END_SRC

** server
#+BEGIN_SRC emacs-lisp
(defun maybe-server-start ()
  (when (eq window-system 'x)
    (server-start)))

(add-hook 'after-init-hook #'maybe-server-start)
#+END_SRC

** imenu
#+BEGIN_SRC emacs-lisp
(setq helm-imenu-type-faces
      '(("^\\(Variables\\|Variable\\|Field\\|Enum Member\\)$" . font-lock-variable-name-face)
        ("^\\(Function\\|Functions\\|Defuns\\|Constructor\\|Method\\)$" . font-lock-function-name-face)
        ("^\\(Types\\|Provides\\|Requires\\|Includes\\|Imports\\|Misc\\|Code\\|Type Parameter\\)$" . font-lock-type-face)
        ("^\\(Classes\\|Class\\|Struct\\|Namespace\\|Other\\)$" . font-lock-doc-face)))
#+END_SRC

** ediff
#+BEGIN_SRC emacs-lisp
(setq ediff-split-window-function 'split-window-horizontally)
(setq ediff-window-setup-function 'ediff-setup-windows-plain)
(setq ediff-restore-winconfig-state-on-exit t) ; my own option

(with-eval-after-load 'ediff-init
  (autoload 'outline-show-all "outline")
  (add-hook 'ediff-prepare-buffer-hook #'outline-show-all))
#+END_SRC

** vdiff
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'vdiff
  (autoload 'outline-show-all "outline")
  (define-key vdiff-mode-map (kbd "C-c d") vdiff-mode-prefix-map)
  (add-hook 'vdiff-mode-hook #'outline-show-all))
#+END_SRC

** xref
#+BEGIN_SRC emacs-lisp
(setq xref-prompt-for-identifier nil)
#+END_SRC

** company
#+BEGIN_SRC emacs-lisp
(setq company-backends
      '(company-capf
        company-files
        company-ispell))

(setq company-idle-delay 0)
(setq company-minimum-prefix-length 3)
(setq company-require-match nil)
(setq company-tooltip-align-annotations t)
(setq company-selection-wrap-around t)
(setq company-replace-completion t)     ; my own option

(with-eval-after-load 'company-dabbrev-code
  (add-to-list 'company-dabbrev-code-modes 'cmake-mode))

(global-company-mode t)
#+END_SRC

** compile
#+BEGIN_SRC emacs-lisp
(setq compilation-read-command nil)
(setq compilation-scroll-output t)
#+END_SRC

** doom-modeline
#+BEGIN_SRC emacs-lisp
(setq doom-modeline-height 24)
(setq doom-modeline-icon nil)

(doom-modeline-mode t)

;;; Use doom's file display in standard modeline
;; (autoload 'doom-modeline-buffer-file-name "doom-modeline-core")
;; (setq-default mode-line-buffer-identification
;;               '(:eval
;;                 (if (buffer-file-name)
;;                     (doom-modeline-buffer-file-name)
;;                   "%b")))
#+END_SRC

** anzu
#+BEGIN_SRC emacs-lisp
(setq anzu-cons-mode-line-p nil)

(global-anzu-mode t)
#+END_SRC

** magit
#+BEGIN_SRC emacs-lisp
(if (eq system-type 'windows-nt)
	(setq magit-git-executable "git.exe"))
(setq magit-repository-directories '(("~/devel/" . 2) ("~/.emacs.d/" . 1) ("~/Documents/" . 1)))
(setq magit-dont-restore-window-configuration t) ; my own option
(setq magit-bury-buffer-function 'quit-window)
(setq magit-define-global-key-bindings nil)
#+END_SRC

** ORG
#+BEGIN_SRC emacs-lisp
(setq org-directory "~/pCloud/Documents/emacs/org")
(defun havner/org-file-path (filename)
  "Return the absolute address of an org file, given its relative name."
  (concat (file-name-as-directory org-directory) filename))
(setq org-index-file (havner/org-file-path "index.org"))

(when (file-exists-p org-index-file)
  (setq org-default-notes-file org-index-file)
  (setq org-agenda-files (list org-index-file))
  (setq org-archive-location (concat (havner/org-file-path "archive.org") "::* From %s")))

(setq org-log-done 'time)
(setq org-edit-src-content-indentation 0)
(setq org-src-fontify-natively t)
(setq org-src-tab-acts-natively t)
(setq org-src-window-setup 'current-window)
(setq org-startup-indented t)
(setq org-support-shift-select t)
(setq org-babel-python-command "python3")
(setq org-confirm-babel-evaluate nil)
(setq org-beamer-theme "Warsaw")
(setq org-highlight-latex-and-related '(latex))
(setq org-export-with-sub-superscripts '{})
(when havner/completing
  (setq org-outline-path-complete-in-steps nil))

(setq org-latex-listings 'minted)
(setq org-latex-packages-alist '(("" "minted")))
(setq org-latex-compiler "xelatex")
(setq org-latex-pdf-process
      '("%latex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "%latex -shell-escape -interaction nonstopmode -output-directory %o %f"
        "%latex -shell-escape -interaction nonstopmode -output-directory %o %f"))

(with-eval-after-load 'org
  (org-babel-do-load-languages 'org-babel-load-languages '((emacs-lisp . t) (python . t) (C . t) (shell . t)))
  (require 'ob-rust)

  (add-hook 'org-mode-hook #'turn-on-auto-fill)
  (add-hook 'org-mode-hook #'org-bullets-mode)

  (require 'ox-twbs)
  (require 'ox-beamer)

  ;;; was (org-cycle-agenda-files), allow avy
  (define-key org-mode-map [(control ?\')] nil))
#+END_SRC

** helm
#+BEGIN_SRC emacs-lisp
(when (eq havner/completing 'helm)
  (defun helm-git-grep-repo (arg)
    "Preconfigured helm for git-grepping the whole repository."
    (interactive "P")
    (require 'helm-grep)
    (helm-grep-git-1 default-directory (not arg)))

  (defun helm-do-find ()
    "`helm-find' with an arg causing to ask for directory by default"
    (interactive)
    (require 'helm-find)
    (helm-find t))

  ;; (setq helm-always-two-windows t)
  ;; (setq helm-split-window-default-side 'right)
  (setq helm-split-window-inside-p t)
  (setq helm-display-buffer-default-height 0.3)

  ;;; for the eshell-pcomplete and other using with-helm-show-completion
  (setq helm-show-completion-display-function #'helm-show-completion-default-display-function)
  (setq helm-show-completion-min-window-height 15)

  (setq helm-candidate-number-limit 1000)
  (setq helm-findutils-search-full-path t)
  (setq helm-bookmark-show-location t)
  (setq helm-buffer-max-length 30)
  (setq helm-ff-search-library-in-sexp t)
  (setq helm-echo-input-in-header-line t)
  (setq helm-ag-insert-at-point 'symbol)

  (with-eval-after-load 'helm-core
    (add-hook 'helm-quit-hook #'havner/kill-buffers-helm))

  (helm-mode t))
#+END_SRC

** symbol-overlay "C-c h"
#+BEGIN_SRC emacs-lisp
(defvar symbol-overlay-command-map
  (let ((map (make-sparse-keymap)))
    (define-key map (kbd "h") #'symbol-overlay-put)
    (define-key map (kbd "r") #'symbol-overlay-remove-all)
    (define-key map (kbd "c") #'symbol-overlay-count)
    (define-key map (kbd "m") #'symbol-overlay-mode)
    (define-key map (kbd "f") #'symbol-overlay-switch-forward)
    (define-key map (kbd "b") #'symbol-overlay-switch-backward)
    (define-key map (kbd "n") #'symbol-overlay-jump-next)
    (define-key map (kbd "p") #'symbol-overlay-jump-prev)
    map))

;; no minor mode, add to global
(define-key global-map (kbd "C-c h") symbol-overlay-command-map)
#+END_SRC

** projectile "C-c p"
#+BEGIN_SRC emacs-lisp
(setq projectile-keymap-prefix (kbd "C-c p"))
(setq projectile-known-projects-file "~/.emacs-projectile.el")
(setq projectile-cache-file "~/.emacs-projectile-cache.el")
(setq projectile-mode-line-prefix " P")
(setq projectile-dynamic-mode-line nil)
(setq frame-title-format '((:eval (projectile-project-name))))

(with-eval-after-load 'projectile
  (add-to-list 'projectile-globally-ignored-directories "build")
  (add-to-list 'projectile-globally-ignored-directories "out")
  (add-to-list 'projectile-globally-ignored-directories ".ccls-cache")
  (add-to-list 'projectile-project-root-files-top-down-recurring "compile_commands.json")
  (add-to-list 'projectile-project-root-files-top-down-recurring ".ccls"))

(projectile-mode t)

(cl-case havner/completing
  ('helm
   (setq projectile-completion-system 'helm)
   (helm-projectile-toggle 1)))
#+END_SRC

** yasnippet "C-c y"
#+BEGIN_SRC emacs-lisp
(setq yas-alias-to-yas/prefix-p nil)

(with-eval-after-load 'yasnippet
  (defvar yas-mode-prefix-map
   (let ((map (make-sparse-keymap)))
     (define-key map "s" 'yas-insert-snippet)
     (define-key map "n" 'yas-new-snippet)
     (define-key map "v" 'yas-visit-snippet-file)
     map))
  (define-key yas-minor-mode-map "\C-c&" nil)
  (define-key yas-minor-mode-map (kbd "C-c y") yas-mode-prefix-map))

(yas-global-mode t)
#+END_SRC

** flycheck "C-c f"
#+BEGIN_SRC emacs-lisp
(setq flycheck-mode-line nil)
(setq flycheck-keymap-prefix (kbd "C-c f"))
(setq flycheck-flake8-maximum-line-length 100)
(setq flycheck-idle-change-delay 3)
(setq flycheck-check-syntax-automatically '(save new-line mode-enabled))

(autoload 'flycheck-select-checker "flycheck")

;;; enable everywhere excluding elisp, it always reports shitload of errors for snippets
(setq-default flycheck-disabled-checkers '(emacs-lisp-checkdoc emacs-lisp))
(global-flycheck-mode t)
#+END_SRC

* Programming modes
#+BEGIN_SRC emacs-lisp
(defun prog-devel-hook-f ()
  (display-line-numbers-mode t)
  (subword-mode t)
  (rainbow-delimiters-mode t)
  (cl-case havner/colors
    ('24bit    (hl-line-mode t))
    ('256color (hl-line-mode t)))
  (setq show-trailing-whitespace t))

(add-hook 'prog-mode-hook #'prog-devel-hook-f t)
#+END_SRC

** LSP
#+BEGIN_SRC emacs-lisp
(defmacro with-real-projectile-file (require-writeable &rest body)
  "Call BODY only if the current buffer is a real file inside a projectile project.
If REQUIRE-WRITEABLE is non-nil the file has to be writeable."
  (declare (debug t))
  `(when (and buffer-file-name
              (projectile-project-root)
              (or (not ,require-writeable)
                  (file-writable-p (buffer-file-name))))
     ,@body))

(setq lsp-session-file "~/.emacs-lsp-session-v1")
(setq lsp-restart 'ignore)
(setq lsp-enable-symbol-highlighting nil)
(setq lsp-keymap-prefix "C-c l")
(setq lsp-headerline-arrow "/")
(setq lsp-file-watch-threshold 5000)
(setq lsp-lens-enable nil)

(setq lsp-ui-doc-enable nil)
(setq lsp-ui-doc-delay 1)
(setq lsp-ui-doc-alignment 'window)
(setq lsp-ui-doc-show-with-cursor t)
(setq lsp-ui-doc-show-with-mouse nil)

(setq lsp-ui-sideline-delay 1)

(with-eval-after-load 'lsp-mode
  (add-to-list 'lsp-file-watch-ignored-directories "[/\\\\]out\\'")
  (add-to-list 'lsp-file-watch-ignored-directories "[/\\\\]build\\'")
  (add-to-list 'lsp-file-watch-ignored-directories "[/\\\\]\\.ccls-cache\\'"))
#+END_SRC

** CC
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'cc-mode
  (require 'ccls))
(setq ccls-executable "~/Documents/ccls/Release/ccls")
;; (setq ccls-initialization-options '(:emitInactiveRegions t :completion (:detailedLabel t)))

(setq c-basic-offset tab-width)
(defvaralias 'c-basic-offset 'tab-width)
(smart-tabs-insinuate 'c 'c++)
(setq c-tab-always-indent nil)
(setq c-insert-tab-function 'completion-at-point)

;; (add-to-list 'auto-mode-alist '("\\.h\\'" . c++-mode))      ; *.h in c++-mode

(setq c-default-style
      '((c-mode . "linux")
        (c++-mode . "stroustrup")
        (java-mode . "java")
        (awk-mode . "awk")
        (other . "gnu")))

(defun cc-devel-hook-f ()
  (c-set-offset 'innamespace 0)
  (c-set-offset 'inextern-lang 0)
  (c-set-offset 'inline-open 0)
  (c-set-offset 'inlambda '+)

  (with-real-projectile-file t (lsp)))

(with-eval-after-load 'cc-vars
  (add-hook 'c-mode-common-hook #'cc-devel-hook-f t))
#+END_SRC

** Rust
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'rust-mode
  (require 'lsp-rust))

(defvaralias 'rust-indent-offset 'tab-width)

(defun rust-devel-f ()
  (setq tab-width 4)
  (setq indent-tabs-mode nil)

  (with-real-projectile-file t (lsp)))

(with-eval-after-load 'rust-mode
  (add-hook 'rust-mode-hook #'rust-devel-f t))
#+END_SRC

** Python
#+BEGIN_SRC emacs-lisp
(setq lsp-pyls-plugins-pylint-enabled nil) ; it's too noisy
(with-eval-after-load 'python
  (require 'lsp-pyls))

(defvaralias 'python-indent-offset 'tab-width)

(defun python-devel-hook-f ()
  (setq tab-width 4)
  (setq indent-tabs-mode nil)

  (with-real-projectile-file t (lsp)))

(with-eval-after-load 'python
  (add-hook 'python-mode-hook #'python-devel-hook-f t))
#+END_SRC

** LUA
#+BEGIN_SRC emacs-lisp
(defvaralias 'lua-indent-level 'tab-width)

(defun lua-devel-hook-f ()
  (setq require-final-newline nil)
  (setq delete-trailing-whitespace-on-save nil)
  (setq tab-width 4))

(with-eval-after-load 'lua-mode
  (add-hook 'lua-mode-hook #'lua-devel-hook-f t))
#+END_SRC

** JS
#+BEGIN_SRC emacs-lisp
(defvaralias 'js-indent-level 'tab-width)

(defun js-devel-f ()
  (setq tab-width 4))

(with-eval-after-load 'js
  (add-hook 'js-mode-hook #'js-devel-f t))
#+END_SRC

** Lisps
#+BEGIN_SRC emacs-lisp
(setq parinfer-extensions
      '(defaults       ; should be included.
         pretty-parens  ; different paren styles for different modes.
         ;; evil           ; If you use Evil.
         ;; lispy          ; If you use Lispy. With this extension, you should install Lispy and do not enable lispy-mode directly.
         ;; paredit        ; Introduce some paredit commands.
         smart-tab      ; C-b & C-f jump positions and smart shift with tab & S-tab.
         smart-yank))   ; Yank behavior depend on mode.

;;; REPLS
(defalias 'run-elisp 'ielm)             ; run-elisp
(setq inferior-lisp-program "sbcl")     ; run-lisp
(setq scheme-program-name "scheme")     ; run-scheme

(defun lisps-devel-hook-f ()
  (setq indent-tabs-mode nil)
  (parinfer-rust-mode t))
(setq lisps-mode-hooks
      '(emacs-lisp-mode-hook
        lisp-mode-hook
        scheme-mode-hook))
        ;; ielm-mode-hook
        ;; inferior-lisp-mode-hook
        ;; inferior-scheme-mode-hook
(dolist (hook lisps-mode-hooks)
  (add-hook hook #'lisps-devel-hook-f t))
#+END_SRC

** shell
#+BEGIN_SRC emacs-lisp
(defvaralias 'sh-indentation 'tab-width)
(defvaralias 'sh-basic-offset 'tab-width)

(add-to-list 'auto-mode-alist '("bashrc\\." . shell-script-mode))
(add-to-list 'auto-mode-alist '("profile\\'" . shell-script-mode))

(defun sh-devel-hook-f ()
  (setq tab-width 4)
  (setq-local company-backends
              '(company-capf
                company-dabbrev-code
                company-files
                company-ispell)))

(with-eval-after-load 'sh-script
  (add-hook 'sh-mode-hook #'sh-devel-hook-f t))
#+END_SRC

** NXML
#+BEGIN_SRC emacs-lisp
(defvaralias 'nxml-child-indent 'tab-width)
(smart-tabs-insinuate 'nxml)

(defun nxml-devel-hook-f ()
  (setq tab-width 2))

(with-eval-after-load 'nxml-mode
  (add-hook 'nxml-mode-hook #'nxml-devel-hook-f t))
#+END_SRC

** Diff
#+BEGIN_SRC emacs-lisp
;;; diff mode resets whitespace-style, my styles include face and trailing
(defun diff-devel-hook-f ()
  (setq-local whitespace-style '(face trailing tab-mark)))

(with-eval-after-load 'diff-mode
  (add-hook 'diff-mode-hook #'diff-devel-hook-f t))
#+END_SRC

** CMake
#+BEGIN_SRC emacs-lisp
(defvaralias 'cmake-tab-width 'tab-width)

(defun cmake-devel-hook-f ()
  (setq-local company-backends
              '((company-dabbrev-code company-cmake)
                company-keywords
                company-files
                company-ispell)))

(with-eval-after-load 'cmake-mode
  (add-hook 'cmake-mode-hook #'cmake-devel-hook-f t))
#+END_SRC

** ansi-term
#+BEGIN_SRC emacs-lisp
(defun term-devel-hook-f ()
  (setq-local transient-mark-mode nil)
  (auto-fill-mode 0)
  (setq term-buffer-maximum-size 0)
  (setq tab-width 8))

(with-eval-after-load 'term
  (add-hook 'term-mode-hook #'term-devel-hook-f t))
#+END_SRC

** eshell
#+BEGIN_SRC emacs-lisp
(defun esh-devel-hook-f ()
  (setq-local company-backends
              '(company-capf)))

(with-eval-after-load 'esh-mode
  (add-hook 'eshell-mode-hook #'esh-devel-hook-f))
#+END_SRC

** LaTeX
#+BEGIN_SRC emacs-lisp
(defun latex-devel-hook-f ()
  (setq-local company-backends
              '(company-files
                company-ispell))
  (company-auctex-init))

(with-eval-after-load 'tex-mode
  (add-hook 'LaTeX-mode-hook #'latex-devel-hook-f))
#+END_SRC

* Shortcuts
** Builtin replacements and similar
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "C-a") #'mwim-beginning-of-line-or-code)     ;; move-end-of-line
(global-set-key (kbd "C-e") #'mwim-end-of-line-or-code)           ;; move-beggining-of-line

(global-set-key (kbd "C-h k") #'helpful-key)                      ;; describe-key
(global-set-key (kbd "C-h f") #'helpful-callable)                 ;; describe-function
(global-set-key (kbd "C-h v") #'helpful-variable)                 ;; describe-variable

(global-set-key (kbd "C-z") #'undo)                               ;; suspend-frame
(global-set-key (kbd "C-S-z") #'undo-redo)                        ;; nil
(global-set-key [remap undo] #'undo-only)                         ;; undo
(global-set-key (kbd "C-x u") #'vundo)                            ;; undo

(global-set-key (kbd "C-x b") #'bs-show)                          ;; switch-to-buffer
(global-set-key (kbd "C-x C-b") #'ibuffer)                        ;; list-buffers
(global-set-key (kbd "C-x d") #'dired-jump)                       ;; dired
(global-set-key (kbd "C-x C-d") #'dired)                          ;; list-directory
(global-set-key (kbd "C-s") #'isearch-forward-regexp)             ;; isearch-forward
(global-set-key (kbd "C-r") #'isearch-backward-regexp)            ;; isearch-backward
(global-set-key (kbd "C-x C-r") #'havner/find-file-as-sudo)       ;; find-file-read-only
(global-set-key (kbd "C-M-s") #'isearch-forward)                  ;; isearch-forward-regexp
(global-set-key (kbd "C-M-r") #'isearch-backward)                 ;; isearch-backward-regexp
(define-key isearch-mode-map (kbd "C-l") #'recenter-top-bottom)   ;; nil

(global-set-key (kbd "C-`") #'multi-vterm-next)                   ;; nil
(global-set-key (kbd "C-x `") #'multi-vterm)                      ;; next-error

(global-set-key (kbd "C-x m") #'magit-status)                     ;; compose-mail
(global-set-key (kbd "C-c m") #'magit-log-head)                   ;; nil
(global-set-key (kbd "C-x g") #'magit-file-dispatch)              ;;
(global-set-key (kbd "C-c g") #'magit-dispatch)                   ;;

(global-set-key (kbd "C-x c") #'ispell-word)                      ;; nil
(global-set-key (kbd "C-x p") #'other-frame)                      ;; project prefix
(global-set-key (kbd "C-x w") #'whitespace-mode)                  ;; nil
(global-set-key (kbd "C-x t") #'toggle-truncate-lines)            ;; tab prefix
(global-set-key (kbd "C-c b") #'bookmark-bmenu-list)              ;; nil

(global-set-key (kbd "C-x <left>") #'window-jump-left)            ;; previous-buffer
(global-set-key (kbd "C-x <right>") #'window-jump-right)          ;; next-buffer
(global-set-key (kbd "C-x <up>") #'window-jump-up)                ;; nil
(global-set-key (kbd "C-x <down>") #'window-jump-down)            ;; nil

(global-set-key (kbd "M-q") #'unfill-toggle)                      ;; fill-paragraph
(global-set-key (kbd "M-z") #'zap-up-to-char)                     ;; zap-to-char
(global-set-key (kbd "C-\\") #'pop-global-mark)                   ;; toggle-input-method
(global-set-key (kbd "M-/") #'xref-find-references)               ;; dabbrev-expand

(global-set-key (kbd "C-x o")   #'switch-window)                                         ;; other-window
(global-set-key (kbd "C-x C-o") #'switch-window-then-swap-buffer)                        ;; delete-blank-lines
(global-set-key (kbd "C-x k")   #'switch-window-then-kill-current-buffer)                ;; kill-buffer
(global-set-key (kbd "C-x C-k") #'switch-window-then-kill-buffer-and-window-and-balance) ;; kbd macro prefix

(global-set-key (kbd "C-x 0") #'switch-window-then-delete-window-and-balance)      ;; delete-window
(global-set-key (kbd "C-x 1") #'switch-window-then-maximize)                       ;; delete-other-windows
(global-set-key (kbd "C-x 2") #'switch-window-then-split-below-switch-and-balance) ;; split-window-below
(global-set-key (kbd "C-x 3") #'switch-window-then-split-right-switch-and-balance) ;; split-window-right
#+END_SRC

** Additional shortcuts
#+BEGIN_SRC emacs-lisp
(global-set-key (kbd "M-n") #'forward-paragraph)
(global-set-key (kbd "M-p") #'backward-paragraph)

(global-set-key (kbd "<s-up>") #'scroll-down-line)
(global-set-key (kbd "<s-down>") #'scroll-up-line)
(global-set-key (kbd "<s-left>") #'window-jump-left)
(global-set-key (kbd "<s-right>") #'window-jump-right)

(global-set-key (kbd "C-x c") #'flyspell-correct-at-point)

(global-set-key (kbd "C-c o") #'windresize)

(global-set-key (kbd "C-c c") #'org-capture)
(global-set-key (kbd "C-c a") #'org-agenda)

(global-set-key (kbd "C-.") #'imenu)
(global-set-key (kbd "C-,") #'imenu-anywhere)

(global-set-key (kbd "C-'") #'avy-goto-word-1)
(global-set-key (kbd "C-;") #'avy-pop-mark)
(define-key isearch-mode-map (kbd "C-'") #'avy-isearch)

(global-set-key (kbd "<f6>") #'find-dired)
(global-set-key (kbd "<f7>") #'ag)
(global-set-key (kbd "<f8>") #'occur)

(define-key prog-mode-map (kbd "C-c C-l") #'symbol-overlay-put)
(define-key prog-mode-map (kbd "C-c C-k") #'symbol-overlay-remove-all)

(with-eval-after-load 'dired
  (define-key dired-mode-map (kbd "<tab>") #'other-window)
  (define-key dired-mode-map (kbd "TAB") #'other-window)
  (define-key dired-mode-map (kbd "<backtab>") #'dired-up-directory))

(with-eval-after-load 'term
  (define-key term-raw-map (kbd "M-x") #'execute-extended-command))

(with-eval-after-load 'esh-mode
  (define-key eshell-mode-map (kbd "<tab>") #'completion-at-point)
  (define-key eshell-mode-map (kbd "TAB") #'completion-at-point))

(with-eval-after-load 'parinfer-rust-mode
  (define-key parinfer-rust-mode-map (kbd "M-'") #'parinfer-rust-toggle-paren-mode))

(with-eval-after-load 'lsp-mode
  (define-key lsp-mode-map (kbd "C-c C-a") #'lsp-execute-code-action)
  (define-key lsp-mode-map (kbd "C-c C-d") #'lsp-describe-thing-at-point)
  (define-key lsp-mode-map (kbd "C-c C-f") #'lsp-format-buffer)
  ;; (define-key lsp-mode-map (kbd "C-c C-l") #'lsp-document-highlight)
  (define-key lsp-mode-map (kbd "C-c C-r") #'lsp-rename))

(with-eval-after-load 'lsp-ui
  (define-key lsp-ui-mode-map (kbd "C-M-,") #'lsp-ui-find-workspace-symbol)
  (define-key lsp-ui-mode-map (kbd "C-M-.") #'lsp-ui-peek-find-definitions)
  (define-key lsp-ui-mode-map (kbd "C-M-/") #'lsp-ui-peek-find-references)
  (define-key lsp-ui-mode-map (kbd "C-c C-c C-c") #'lsp-ui-flycheck-list))

(with-eval-after-load 'company
  (define-key company-active-map [return] nil)
  (define-key company-active-map (kbd "RET") nil)
  (define-key company-active-map (kbd "<tab>") #'company-complete-selection)
  (define-key company-active-map (kbd "TAB") #'company-complete-selection))

(with-eval-after-load 'company-template
  (define-key company-template-nav-map [tab] nil)
  (define-key company-template-nav-map (kbd "TAB") nil)
  (define-key company-template-nav-map (kbd "<backtab>") #'company-template-forward-field)
  (define-key company-template-nav-map (kbd "S-<tab>") #'company-template-forward-field))

(with-eval-after-load 'yasnippet
  (define-key yas-keymap [(tab)] nil)
  (define-key yas-keymap (kbd "TAB") nil)
  (define-key yas-keymap (kbd "<backtab>") 'yas-next-field-or-maybe-expand)
  (define-key yas-keymap (kbd "S-<tab>") 'yas-next-field-or-maybe-expand))

(with-eval-after-load 'projectile
  (define-key projectile-mode-map (kbd "C-c C-g") #'vc-git-grep)
  (define-key projectile-mode-map (kbd "C-c C-s") #'projectile-ag)
  (define-key projectile-mode-map (kbd "<f5>") #'projectile-compile-project)
  (define-key projectile-mode-map (kbd "<f9>") #'projectile-commander)
  (define-key projectile-command-map (kbd "<SPC>") #'projectile-commander))

(with-eval-after-load 'flycheck
  (define-key flycheck-mode-map (kbd "<f10>") #'flycheck-list-errors)
  (define-key flycheck-command-map (kbd "<SPC>") #'flycheck-list-errors))

(with-eval-after-load 'diff-mode
  (define-key diff-mode-map (kbd "C-c C-d") #'diffview-current))
#+END_SRC

** Completing
#+BEGIN_SRC emacs-lisp
(with-eval-after-load 'helm
  (define-key helm-map (kbd "<tab>") #'helm-execute-persistent-action)
  (define-key helm-map (kbd "TAB") #'helm-execute-persistent-action)
  (define-key helm-map (kbd "<backtab>") #'helm-find-files-up-one-level)
  (define-key helm-map (kbd "C-z") #'helm-select-action)
  (define-key helm-map (kbd "C-<tab>") #'helm-next-source))

(cl-case havner/completing
  ('helm
   (global-set-key (kbd "M-x") #'helm-M-x)
   (global-set-key (kbd "M-y") #'helm-show-kill-ring)
   (global-set-key (kbd "C-h a") #'helm-apropos)
   (global-set-key (kbd "C-h m") #'helm-describe-modes)
   (global-set-key (kbd "C-h b") #'helm-descbinds)
   (global-set-key (kbd "C-x C-f") #'helm-find-files)
   (global-set-key (kbd "C-x b") #'helm-mini)
   (global-set-key (kbd "C-c b") #'helm-bookmarks)

   (global-set-key (kbd "<f1>") #'helm-resume)
   (global-set-key (kbd "<f6>") #'helm-do-find)
   (global-set-key (kbd "<f7>") #'helm-do-ag)
   (global-set-key (kbd "<f8>") #'helm-occur)
   (global-set-key (kbd "C-.") #'helm-imenu)
   (global-set-key (kbd "C-,") #'helm-imenu-anywhere)
   (global-set-key (kbd "C-h SPC") #'helm-all-mark-rings)
   (global-set-key (kbd "C-x r SPC") #'helm-register)))

(with-eval-after-load 'term
  (cl-case havner/completing
    ('helm
     (define-key term-raw-map (kbd "M-x") #'helm-M-x)
     (define-key term-raw-escape-map (kbd "M-x") #'helm-M-x))))

(with-eval-after-load 'company
  (cl-case havner/completing
    ('helm
     (define-key company-active-map (kbd "C-<tab>") #'helm-company))))

(with-eval-after-load 'projectile
  (cl-case havner/completing
    ('helm
     (define-key projectile-mode-map (kbd "C-c C-g") #'helm-git-grep-repo)
     (define-key projectile-mode-map (kbd "C-c C-s") #'helm-projectile-ag)
     (define-key projectile-mode-map (kbd "<f9>") #'helm-projectile)
     (define-key projectile-command-map (kbd "<SPC>") #'helm-projectile))))

(with-eval-after-load 'flycheck
  (cl-case havner/completing
    ('helm
     (define-key flycheck-mode-map (kbd "<f10>") #'helm-flycheck)
     (define-key flycheck-command-map (kbd "<SPC>") #'helm-flycheck))))

(with-eval-after-load 'esh-mode
  (cl-case havner/completing
    ('helm
     ;; (define-key eshell-mode-map (kbd "<tab>") #'helm-esh-pcomplete)
     ;; (define-key eshell-mode-map (kbd "TAB") #'helm-esh-pcomplete)
     (define-key eshell-mode-map (kbd "C-c C-l") #'helm-eshell-history))))
#+END_SRC

* Notes
** C-x keys:
| key     | orig                 | new                                                   | change |
|---------+----------------------+-------------------------------------------------------+--------|
| C-0     |                      |                                                       |        |
| C-1     |                      |                                                       |        |
| C-2     |                      |                                                       |        |
| C-3     |                      |                                                       |        |
| C-4     |                      |                                                       |        |
| C-5     |                      |                                                       |        |
| C-6     |                      |                                                       |        |
| C-7     |                      |                                                       |        |
| C-8     |                      |                                                       |        |
| C-9     |                      |                                                       |        |
|---------+----------------------+-------------------------------------------------------+--------|
| C-a     |                      |                                                       |        |
| C-b     | list-buffers         | ibuffer                                               |        |
| C-d     | list-directory       | dired                                                 |        |
| C-g     |                      |                                                       |        |
| C-j     | dired-jump           |                                                       |        |
| C-k     | kbd macro prefix     | switch-window-then-kill-buffer-and-window-and-balance | +      |
| C-o     | delete-blank-lines   | switch-window-then-swap-buffer                        | +      |
| C-r     | find-file-read-only  | havner/find-file-as-sudo                              | +      |
| C-y     |                      |                                                       |        |
|---------+----------------------+-------------------------------------------------------+--------|
| 0       | delete-window        | switch-window-then-delete-window-and-balance          |        |
| 1       | delete-other-windows | switch-window-then-maximize                           |        |
| 2       | split-window-below   | switch-window-then-split-below-switch-and-balance     |        |
| 3       | split-window-right   | switch-window-then-split-right-switch-and-balance     |        |
| 7       |                      |                                                       |        |
| 9       |                      |                                                       |        |
|---------+----------------------+-------------------------------------------------------+--------|
| `       | next-error           | multi-vterm-next                                      | +      |
| b       | switch-to-buffer     | bs-show                                               |        |
| c       |                      | ispell-word                                           | +      |
| d       | dired                | dired-jump                                            |        |
| g       |                      | magit-file-popup                                      | +      |
| j       |                      |                                                       |        |
| k       | kill-buffer          | switch-window-then-kill-current-buffer                |        |
| m       | compose-mail         | magit-status                                          | +      |
| o       | other-window         | switch-window                                         |        |
| p       | project prefix       | other-frame                                           | +      |
| t       | tab prefix           | toggle-truncate-lines                                 | +      |
| u       | undo                 | vundo                                                 |        |
| w       |                      | whitespace-mode                                       | +      |
| y       |                      |                                                       |        |
| <up>    |                      | window-jump-up                                        | +      |
| <down>  |                      | window-jump-down                                      | +      |
| <left>  | previous-buffer      | window-jump-left                                      | +      |
| <right> | next-buffer          | window-jump-right                                     | +      |
|---------+----------------------+-------------------------------------------------------+--------|

** Navigation

left/right:
  - char
  - word
next/previous:
  - line
forward/backward:
  - char
  - word
  - line
  - sentence
  - paragraph

|-------+------------+-----------------|
|       | Ctrl       | Meta            |
|-------+------------+-----------------|
| f     | forw char  | forw word       |
| b     | back char  | back word       |
| n     | next line  | forw paragraph* |
| p     | prev line  | back paragraph* |
| e     | line end   | forw sentence   |
| a     | line begin | back sentence   |
|-------+------------+-----------------|
|       | (none)     | Ctrl            |
|-------+------------+-----------------|
| right | right char | right word      |
| left  | left char  | left word       |
| down  | next line  | forw paragraph  |
| up    | prev line  | back paragraph  |
|-------+------------+-----------------|
